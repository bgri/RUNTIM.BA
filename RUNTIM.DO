10 'BATTERY PACK RUNTIME TEST 
15 'SIMPLE TEST - LOOP UNTIL NM$<>CM$
20 'WHEN NM$<>CM$ A MINUTE HAS PASSED
25 'ONCE A MINUTE,SAVE THE RUNTIME
30 'VALUE AND CALCULATIONS TO A RAM FILE
31 CLS:?"BATTERY PACK RUNTIME TEST v.1":?"Brad Grier, 2021"
32 ?"PRESS A KEY TO CONTINUE"
39 K$=INKEY$:IF K$="" THEN 39
40 FN$="RUNOUT.DO":'OUTPUT FILE NAME
50 CM$=" ":'CURRENT MINUTE
60 NM$=" ":'NEW MINUTE
70 I=0:' ITERATIONS (FILES SAVED)
80 IT$=TIME$:'TIME PROGRAM STARTED
90 OU$=" ":'TIME FILE WRITTEN TO RAM
100 CLS
199 NM$=MID$(TIME$,4,2):'NM$ INITIALIZED
200 ' LOOPING CODE STARTS HERE
300 CM$= MID$(TIME$,4,2):'SET CM$ 
400 IF VAL(NM$)=VAL(CM$)GOTO 999:'COMPARE
405 'AND LOOP IF EQUAL
600 'SAVE ROUTINE
610 CLS:'IF NOT EQUAL
620 I=I+1:'INCREMENT SAVE COUNTER
630 OU$=TIME$:'GET TIME OF SAVE
690 '
691 PRINT"FILENAME=";:PRINT FN$:PRINT"SAVED.":'TO SCREEN
693 OPEN FN$ FOR OUTPUT AS #1:'OPEN RAM FILE AND DUMP DATA
694 PRINT#1, DATE$ + " I = ";+STR$(I):PRINT #1,"IN:"+IT$+" OUT:"+TIME$
695 PRINT#1, "PACK LIFE"+STR$(INT(I/60))+"hr,"+STR$(I MOD 60)+"mn."
699 CLOSE:BEEP:GOTO 199:'CLOSE RAM FILE AND LOOP TO RESET NM$ TO NEXT MINUTE'
999 PRINT"=="+STR$(I)+" ";:GOTO 200:'UPDATE SCREEN AND LOOP
2000 'DUMP VARIABLES - DEBUG STUFF - NOT CALLED
2010 PRINT"FILENAME:"+ FN$
2020 PRINT"CM:"+ CM$
2030 PRINT"NM:"+ NM$
2033 PRINT"IT:"+ IT$
2035 PRINT"OU:"+ OU$
2040 PRINT "ITERATION:";I
